/* Author: Hao Wu 
 * A simple state machine language for specifying state machines.
 * 03-July-2020
 */
grammar StateMachine;
options {
    superClass = BaseParser;
    backtrack=true;
    memoize=true;
}

@header{
package org.nuim.cyclone.parser;
import java.util.Collections;
import java.util.Arrays;
import org.nuim.cyclone.parser.ast.*;
import org.nuim.cyclone.model.type.*;
import org.nuim.cyclone.model.value.*;
}

@lexer::header {
package org.nuim.cyclone.parser;
}

@lexer::members {
    private ParseErrorHandler fParseErrorHandler;

    public String getFilename() {
        return fParseErrorHandler.getFileName();
    }
    
    @Override
    public String getErrorHeader(RecognitionException e) {
    	return "line " + e.line + ":" + e.charPositionInLine;
    }
    	
    public void emitErrorMessage(String msg) {
        fParseErrorHandler.reportError(msg);
    }
 
    public void init(ParseErrorHandler handler) {
        fParseErrorHandler = handler;
    }
}

machine returns [ASTMachine machine]:

    MACHINE name=identifier {$machine = new ASTMachine(name);} LBRACE
        (v=globalVariableDecl {$machine.addVariable(v);})*
        (state)* (trans)*
        (invariantExpression)*
    RBRACE EOF
;

state:
    (stateModifier) ? STATE identifier LBRACE
        (localVariableDecl)*
        (expression SEMI)*
    RBRACE
;

trans: TRANS identifier LBRACE
        identifier ARROW identifier ON label
    RBRACE
;

invariantExpression:
    INVARIANT 
        (identifier) ? 
    LBRACE 
        (expression SEMI)+ 
    RBRACE
;

label:
    STRINGLITERAL
;

identifier returns [String name]:
    str=IDENT {$name=$str.getText();}
;

stateModifier: 
      START 
    | FINAL
;

literal:
      INTLITERAL
    | REALLITERAL
    | BOOLLITERAL
    | STRINGLITERAL
    | CHARLITERAL
;

globalVariableDecl returns [ASTVariable v]:
    t=type n=variableDeclarator {n.type=t;n.createVariable();$v=n;}
    SEMI
;

localVariableDecl :
    type variableDeclarator
    SEMI
;

modifier :
        'global'
    |   'native'
;

type returns [Type t] :
        p=primitiveType {$t=p;}
    |   e=enumType{$t=e;}
;

primitiveType returns [Type t]:
      INT {$t= new IntType();}
    | BOOL {$t=new BoolType();}
    | REAL {$t=new RealType();} 
    | STRING {$t=new StringType();}
;

enumType returns [EnumType t] :
    ENUM {$t=new EnumType();} LBRACE name=identifier {$t.add(name);} (COMMA l=identifier {$t.add(l);})* RBRACE
;

variableDeclarator returns [ASTVariable var]:
    {$var=new ASTVariable();}
    n=identifier {$var.name=n;}
    ('=' variableInitializer)? 
    (WHERE expression) ?
;

expression:
        conditionalOrExpression
        (assignmentOperator expression
        )? 
    ;

assignmentOperator 
    :   '='
;

conditionalOrExpression 
    :   conditionalAndExpression
        ('||' conditionalAndExpression
        )*
    ;

conditionalAndExpression
    :
    relationalExpression 
     ('&&' relationalExpression
        )*
    ;

relationalExpression 
    : additiveExpression
        (relationalOp additiveExpression
        )*
;

relationalOp 
    :    '<=' 
    |    '>='
    |   '<'
    |   '>'
    ;

additiveExpression 
    :   multiplicativeExpression
        (   
            (   '+'
            |   '-'
            )
            multiplicativeExpression
         )*
    ;

multiplicativeExpression 
    :
        unaryExpression
        (   
            (   '*'
            |   '/'
            |   '%'
            )
            unaryExpression
        )*
    ;

unaryExpression 
    :   '+'  unaryExpression
    |   '-' unaryExpression
    |   '++' unaryExpression
    |   '--' unaryExpression
    |   unaryExpressionNotPlusMinus
    ;

unaryExpressionNotPlusMinus 
    :   '!' unaryExpression
    |   primary
    ;

primary 
    :   parExpression 
    |   identifier
    |   literal
    ;

variableInitializer: 
        expression
    ;

parExpression 
    :   '(' expression ')'
    ;